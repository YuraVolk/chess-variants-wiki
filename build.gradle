buildscript {
	ext {
		springBootVersion = '2.7.0'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

plugins {
	id "com.github.node-gradle.node" version "3.1.1"
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group = 'com.example'
version = '1.0'
sourceCompatibility = '1.8'

repositories {
	mavenCentral()
}

node {
	download = true
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-jdbc'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'com.google.firebase:firebase-admin:9.2.0'
	runtimeOnly 'org.postgresql:postgresql'
	runtimeOnly 'org.webjars:webjars-locator:0.47'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

npmInstall {
	onlyIf { !gradle.startParameter.taskNames.contains('bootRun') }
}

task buildApp(type: NodeTask, dependsOn: 'npmInstall') {
	script = project.file('node_modules/webpack/bin/webpack.js')
	args = [
			'--mode', 'production',
			'--entry', './src/main/client/ts/Main',
			'-o', './src/main/resources/public/scripts/'
	]
	onlyIf { !gradle.startParameter.taskNames.contains('bootRun') }
}

processResources.dependsOn 'buildApp'
clean.delete << file('node_modules')
clean.delete << file ('src/main/resources/public/scripts/')
clean.delete << file ('src/main/resources/public/assets/')
clean.delete << file ('src/main/resources/public/fonts/')
